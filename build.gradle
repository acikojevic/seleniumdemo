apply plugin: 'java'
apply plugin: 'maven'

group = 'com.acikojevic.testautomation'
version = '1.0-SNAPSHOT'

description = "selenium demo using gradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
    
}

dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: version_selenium
    compile group: 'org.seleniumhq.selenium', name: 'selenium-server', version: version_selenium
    compile group: 'org.testng', name: 'testng', version: version_testng
}

sourceSets {

    test {
        java {
            srcDir 'src/main/java'
        }
    }
}

task startHub {

	doLast {
		println("Starting selenium hub...")		
        ant.java(jar: selenium_standalone, spawn: true, fork: true) {
        	arg(value: "-role")
        	arg(value: "hub")             
   		} 
   	}  
}

task startNode(dependsOn: startHub) {
    		
    doLast {
    	println("Starting selenium node...")	
        ant.java(jar: selenium_standalone, spawn: true, fork: true) {
        	arg(value: "-role")
        	arg(value: "node")
        	arg(value: "-hub")
        	arg(value: "http://localhost:4444/grid/register") 
        	arg(value: "-servlet")
        	arg(value: "org.openqa.grid.web.servlet.LifecycleServlet")    
  		}     
     }  
}

task stopNode {
    
	doLast {
		def stopNodeURL = "http://localhost:5555/extra/LifecycleServlet?action=shutdown".toURL()
		println("Shutting down node: " + stopNodeURL)
		stopNodeURL.text
	}
}

task stopHub(dependsOn: stopNode) {
	
	doLast {
		def stopHubURL = "http://localhost:4444/lifecycle-manager?action=shutdown".toURL()
		println("Shutting down hub: " + stopHubURL)
		stopHubURL.text
	}
}

task runTests(type: Test, dependsOn: startNode) {

	finalizedBy stopHub
	
	testLogging.showStandardStreams = true	
	useTestNG()
	{
		suites "testNG.yaml"
	}
}

